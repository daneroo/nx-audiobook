# Install just on Ubuntu:
# curl --proto '=https' --tlsv1.2 -sSf https://just.systems/install.sh | sudo bash -s -- --to /usr/local/bin/

# List available commands
default:
    just -l

alias ps := status
# Status (docker compose)
status:
    docker compose ps


# Start in development mode
start-dev:
    @echo "Starting in development mode...";
    AUDIOBOOKS_VOLUME={{justfile_directory()}}/data/audiobooks AUDIOBOOKS_ACCESS_MODE=rw docker compose up -d

# Start in production mode
start-prod:
    @echo "Starting in production mode...";
    docker compose up -d

# The start target is a single line continued command, so that ENV_VARS can be set
# Start in production mode - or choose dev/prod when on galois
start:
    @if [ "$(hostname -s)" = "galois" ]; then \
        echo "Running on galois, you probably want dev"; \
        TYPE=$(gum choose "dev" "prod"); \
        if [ "$TYPE" = "dev" ]; then \
            just start-dev; \
            exit 0; \
        fi \
    fi; \
    just start-prod

# Stop the application
stop:
    docker compose down

# Exec into the container
exec:
    docker compose exec audiobookshelf ash
# Show logs
logs:
    docker compose logs -f    